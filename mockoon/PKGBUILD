# Maintainer: Daniele Sluijters <daenney@users.noreply.github.com>

pkgname=mockoon-appimage
pkgver=1.3.0
pkgrel=4
pkgdesc="Create and run mock APIs locally."
arch=('x86_64')
url="https://github.com/255kb/mockoon"
license=('MIT')
depends=(openssl zlib)
_filename=mockoon-${pkgver}-x86_64.AppImage
source=(
  "https://github.com/255kb/mockoon/releases/download/v${pkgver}/${_filename}"
  "mockoon.desktop.patch"
)
sha256sums=(
  9b2d493bcfd350731105d93e944d98381763b7e452bee2d703fbacd1e5f85fb4
  43426a62309dbdca55af977723f576d7252ca9c3c1c7d5b7cacf3716040caec1
)
options=(!strip)

prepare() {
  cd "${srcdir}"
  chmod +x ${_filename}
  ./${_filename} --appimage-extract
  patch -Np0 -i mockoon.desktop.patch
}

package() {
  install -Dm755 "${srcdir}/${_filename}" "${pkgdir}/opt/appimages/mockoon.AppImage"
  install -dm755 "${pkgdir}/usr/share/"
  cp -r --no-preserve=mode,ownership "${srcdir}/squashfs-root/usr/share/icons" "${pkgdir}/usr/share/"

  install -Dm644 "${srcdir}/squashfs-root/mockoon.desktop" "${pkgdir}/usr/share/applications/mockoon.desktop"

  for f in $(find -type f -name 'LICENSE*'); do
    install -D -m0644 "$f" "$pkgdir/usr/share/licenses/$pkgname/${f#./squashfs-root/}"
  done
}

# vim:set ts=2 sw=2 et:
