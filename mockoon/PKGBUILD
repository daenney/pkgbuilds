# Maintainer: Daniele Sluijters <daenney@users.noreply.github.com>

pkgname=mockoon-appimage
pkgver=1.3.0
pkgrel=3
pkgdesc="Create and run mock APIs locally."
arch=('x86_64')
url="https://github.com/255kb/mockoon"
license=('MIT')
depends=(openssl zlib)
_filename=mockoon-${pkgver}-x86_64.AppImage
source=("https://github.com/255kb/mockoon/releases/download/v${pkgver}/${_filename}"
        'mockoon.desktop'
)
sha256sums=(
  9b2d493bcfd350731105d93e944d98381763b7e452bee2d703fbacd1e5f85fb4
  469a1cfdfd70a2f85150d0802fe10e7bd3dfe20ec609416515dbb0904e17d735
)
options=(!strip)

prepare() {
  cd "${srcdir}"
  chmod +x ${_filename}
  ./${_filename} --appimage-extract
}

package() {
  install -Dm755 "${srcdir}/${_filename}" "${pkgdir}/opt/appimages/mockoon.AppImage"
  install -dm755 "${pkgdir}/usr/share/"
  cp -r --no-preserve=mode,ownership "${srcdir}/squashfs-root/usr/share/icons" "${pkgdir}/usr/share/"

  install -Dm644 "${srcdir}/mockoon.desktop" "${pkgdir}/usr/share/applications/mockoon.desktop"

  for f in $(find -type f -name 'LICENSE*'); do
    install -D -m0644 "$f" "$pkgdir/usr/share/licenses/$pkgname/$(basename $f)"
	done
}

# vim:set ts=2 sw=2 et:
