# Maintainer: Daniele Sluijters <daenney@users.noreply.github.com>

pkgname=veonim-appimage
pkgver=0.22.0
pkgrel=1
pkgdesc="Simple modal IDE built on neovim "
arch=('x86_64')
url="https://github.com/veonim/veonim"
license=('MIT')
depends=(openssl zlib)
_filename=veonim.${pkgver}.AppImage
source=(
  "https://github.com/veonim/veonim/releases/download/${pkgver}/${_filename}"
  "veonim.desktop.patch"
)
sha256sums=(
  6da1eb1fa354e0690472ff578886a0f00302d2b11750f4d5fc48a71b66284d64
  b768b84d414bb615c8426d36d63d1414022a5e926c2f7b4bc8eb8200c64da308
)
options=(!strip)

prepare() {
  cd "${srcdir}"
  chmod +x ${_filename}
  ./${_filename} --appimage-extract
  patch -Np0 -i veonim.desktop.patch
}

package() {
  install -Dm755 "${srcdir}/${_filename}" "${pkgdir}/opt/appimages/veonim.AppImage"
  install -Dm644 "${srcdir}/squashfs-root/usr/share/icons/hicolor/0x0/apps/veonim.png" "${pkgdir}/usr/share/icons/hicolor/256x256/apps/veonim.png"
  install -Dm644 "${srcdir}/squashfs-root/veonim.desktop" "${pkgdir}/usr/share/applications/veonim.desktop"

  for f in $(find -type f -name 'LICENSE*'); do
    install -D -m0644 "$f" "$pkgdir/usr/share/licenses/$pkgname/${f#./squashfs-root/}"
  done
}

# vim:set ts=2 sw=2 et:
