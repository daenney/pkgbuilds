# Maintainer: Daniele Sluijters <daenney@users.noreply.github.com>
pkgname=dell-smm-hwmon-lts
pkgver=4.14.32
pkgrel=1
pkgdesc="Patched version of the Dell SMM hwmon kernel module. This allows to disable the BIOS
fan controller on some Dell laptops so that the i8kutils driver can work without conflicts."
url='https://github.com/vitorafsr/i8kutils/issues/6/'
arch=('x86_64')
license=('GPL')
optdepends=('dell-smm-hwmon-cli: CLI to send SMM control commands'
            'i8kutils: Fan control for Dell laptops')
makedepends=('bc' "linux-lts=${pkgver}-${pkgrel}" "linux-lts-headers=${pkgver}-${pkgrel}")
conflicts=('dell-smm-hwmon-i8kutils')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
        "https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.sign"
        'dell-smm-hwmon.patch'
        'dell-smm-hwmon-lts.hook')
sha256sums=('SKIP'
            'SKIP'
            '52ee6a4558e9b5e4e3fe9bc2433baa67cde1190b2be846c649368ba796ad09d7'
            '0790c92800513f2e2e3697833ffed28c5fadb0af208bd7e9d2f589ae04ec5e27')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'
              '647F28654894E3BD457199BE38DBBDC86092693E')


prepare() {
  cd "$srcdir/linux-$pkgver"
  patch -p0 < ../dell-smm-hwmon.patch
  make mrproper
  cp "/usr/lib/modules/${pkgver}-${pkgrel}-lts/build/.config" ./
  cp "/usr/lib/modules/${pkgver}-${pkgrel}-lts/build/Module.symvers" ./
}

build() {
  cd "$srcdir/linux-$pkgver"
  EXTRAVERSION=-${pkgrel} make modules_prepare
  make M=drivers/hwmon
  gzip ./drivers/hwmon/dell-smm-hwmon.ko
}

package() {
  mkdir -p "${pkgdir}/usr/lib/modules/${pkgver}-${pkgrel}-lts/updates/"
  cp "${srcdir}/linux-$pkgver/drivers/hwmon/dell-smm-hwmon.ko.gz" "${pkgdir}/usr/lib/modules/${pkgver}-${pkgrel}-lts/updates/"

  # install pacman hook
  install -Dm644 dell-smm-hwmon-lts.hook "${pkgdir}/usr/share/libalpm/hooks/dell-smm-hwmon-lts.hook"
}

